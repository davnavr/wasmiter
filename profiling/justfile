#!/usr/bin/env -S just --justfile

wasmiter_dis := justfile_directory() + "/../target/profiling/wasmiter-dis"

list:
    @just --list --justfile {{ justfile() }}

clean *FLAGS='--target wasm32-wasi --profile default':
    cargo clean {{ FLAGS }}

clippy *FLAGS='--profile default':
    cargo clippy --target wasm32-wasi {{ FLAGS }}

fmt:
    cargo fmt

build *FLAGS='--profile default':
    cargo build --target wasm32-wasi {{ FLAGS }}

build_dis:
    cd ../ && cargo build --package wasmiter-dis --profile profiling --quiet

# Records an invocation of wasmiter-dis with the specified target using samply
samply_dis target profile='default' *FLAGS='': build_dis (build '--package ' + target + ' --profile ' + profile)
    wasm="{{ justfile_directory() }}/target/wasm32-wasi/{{ profile }}/{{ target }}.wasm" && samply record --no-open {{ wasmiter_dis }} $wasm {{ FLAGS }} > /dev/null
